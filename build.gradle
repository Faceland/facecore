import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "com.github.johnrengelman.shadow" version "1.2.0"
    id "java"
    id "maven-publish"
    id "wrapper"
}

sourceCompatibility = "1.7"

defaultTasks 'clean', 'licenseFormat', 'check', 'build'

group = "com.tealcube.minecraft.bukkit"
archivesBaseName = "facecore"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "http://tealcube.com/nexus/content/groups/public/"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        url "http://ci.shadowvolt.com/plugin/repository/everything/"
    }
}

dependencies {
    compile("com.comphenix.protocol:ProtocolLib:3.6.3-SNAPSHOT") {
        exclude module: "BukkitExecutors"
    }
    compile "org.apache.commons:commons-pool2:2.2"
    compile "org.apache.commons:commons-dbcp2:2.0.1"
    compile "commons-logging:commons-logging:1.2"
    compile "com.tealcube.minecraft.bukkit:kern-all:1.0.0-SNAPSHOT"
    compile "com.tealcube.minecraft.bukkit:hilt:1.0.0-SNAPSHOT"
    compile "com.tealcube.minecraft.bukkit:config:1.0.0-SNAPSHOT"
    compile "com.tealcube.minecraft.bukkit:mirror:1.0.0-SNAPSHOT"
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("BUILD_NUMBER") ?: "0"),
            NAME   : project.name
    ]
}

shadowJar {
    baseName = "facecore-all"
    classifier = ""

    relocate "org.apache", "com.tealcube.minecraft.bukkit.facecore.shade.apache"
    relocate "com.tealcube.minecraft.bukkit.hilt", "com.tealcube.minecraft.bukkit.facecore.shade.hilt"
    relocate "com.tealcube.minecraft.bukkit.config", "com.tealcube.minecraft.bukkit.facecore.shade.config"
    relocate "com.tealcube.minecraft.bukkit.mirror", "com.tealcube.minecraft.bukkit.facecore.shade.reflect"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

license {
    header file('HEADER')
    include "com/tealcube/minecraft/bukkit/facecore/**/*.java"
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "facecore"
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
        shadow(MavenPublication) {
            artifactId = "facecore-all"
            from components.shadow
        }
    }
    repositories {
        maven {
            url "http://tealcube.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "TealCube"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}

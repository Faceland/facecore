import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "com.github.johnrengelman.shadow" version "1.2.0"
    id "java"
    id "maven"
    id "maven-publish"
    id "wrapper"
}

sourceCompatibility = "1.7"

defaultTasks 'clean', 'licenseFormat', 'check', 'build'

group = "org.nunnerycode"
archivesBaseName = "facecore-gradle"
version = "0.0.2"

repositories {
    mavenCentral()
    maven {
        url "http://repo-topplethenun.rhcloud.com/nexus/content/groups/public/"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        url "http://repo.comphenix.net/content/groups/public/"
    }
}

dependencies {
    compile "org.nunnerycode:kern-all:0.0.5-SNAPSHOT"
    compile "com.comphenix.attribute:AttributeStorage:0.0.2-SNAPSHOT"
    compile "org.apache.commons:commons-pool2:2.2"
    compile "org.apache.commons:commons-dbcp2:2.0.1"
    compile "commons-logging:commons-logging:1.2"
    compile "com.h2database:h2:1.3.170"
    compile "me.topplethenun:hilt:1.0.1"
    compile "me.topplethenun:config:latest.integration"
    compile "me.topplethenun:mirror:latest.integration"
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("CIRCLECI") != null ? "circleci" : "compiled") + "-" + (System.getenv("CIRCLE_BUILD_NUM") ?: "0"),
            NAME   : project.name
    ]
}

shadowJar {
    baseName = "facecore-gradle-all"
    classifier = ""

    relocate "com.comphenix", "org.nunnerycode.facecore.comphenix"
    relocate "org.apache", "org.nunnerycode.facecore.apache"
    relocate "com.h2database", "org.nunnerycode.facecore.h2"
    relocate "me.topplethenun.hilt", "org.nunnerycode.facecore.hilt"
    relocate "me.topplethenun.config", "org.nunnerycode.facecore.configuration"
    relocate "me.topplethenun.reflect", "org.nunnerycode.facecore.reflect"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

license {
    header file('HEADER')
    include "org/nunnerycode/facecore/**/*.java"
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "facecore-gradle"
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
        shadow(MavenPublication) {
            artifactId = "facecore-gradle-all"
            from components.shadow

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
    repositories {
        maven {
            url "http://repo-topplethenun.rhcloud.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "nunnerycode-repo"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}

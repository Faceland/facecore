import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "com.github.hierynomus.license" version "0.11.0"
    id "pl.allegro.tech.build.axion-release" version "1.2.4"
    id "com.github.johnrengelman.shadow" version "1.2.1"
    id "signing"
}

sourceCompatibility = 1.7

scmVersion {
    tag {
        prefix = project.name
    }
    createReleaseCommit true
    releaseCommitMessage { version, position -> "[ci skip] release version: " + version}
}

group = "com.tealcube.minecraft.bukkit"
version = scmVersion.version

apply from: "publish-java.gradle"

repositories {
    mavenCentral()
    maven {
        url "http://maven.tealcube.com/artifactory/public"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        url "http://ci.shadowvolt.com/plugin/repository/everything/"
    }
    maven {
        url "http://repo.greatmancode.com/content/groups/public/"
    }
}

dependencies {
    compile "org.apache.commons:commons-pool2:2.2"
    compile "org.apache.commons:commons-dbcp2:2.0.1"
    compile "commons-logging:commons-logging:1.2"
    compile "com.tealcube.minecraft.bukkit:kern:0.1.3:all"
    compile "com.tealcube.minecraft.bukkit:hilt:2.0.1"
    compile "com.tealcube.minecraft.bukkit:config:0.1.6-SNAPSHOT"
    compile "com.tealcube.minecraft.bukkit:mirror:0.1.1-SNAPSHOT"
    compile "commons-validator:commons-validator:1.4.1"
    compile "com.tealcube.minecraft.bukkit:AmpMenus:0.1.1-SNAPSHOT"
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("BUILD_NUMBER") ?: "0"),
            NAME   : System.getenv("JOB_NAME") ?: project.archivesBaseName
    ]
}

shadowJar {
    relocate "ninja.amp", "com.tealcube.minecraft.bukkit.facecore.shade.amp"
    relocate "org.apache", "com.tealcube.minecraft.bukkit.facecore.shade.apache"
    relocate "com.tealcube.minecraft.bukkit.hilt", "com.tealcube.minecraft.bukkit.facecore.shade.hilt"
    relocate "com.tealcube.minecraft.bukkit.config", "com.tealcube.minecraft.bukkit.facecore.shade.config"
    relocate "com.tealcube.minecraft.bukkit.mirror", "com.tealcube.minecraft.bukkit.facecore.shade.reflect"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

license {
    header file('HEADER')
    include "com/tealcube/minecraft/bukkit/facecore/**/*.java"
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
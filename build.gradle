import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "com.github.johnrengelman.shadow" version "1.2.0"
    id "java"
    id "maven"
    id "maven-publish"
    id "wrapper"
}

sourceCompatibility = "1.7"

defaultTasks 'clean', 'licenseFormat', 'check', 'build'

group = "org.nunnerycode"
archivesBaseName = "facecore-gradle"
version = "0.0.1-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "http://repo-topplethenun.rhcloud.com/nexus/content/groups/public/"
    }
    maven {
        url "http://repo.comphenix.net/content/groups/public/"
    }
}

dependencies {
    compile "org.nunnerycode:kern-all:0.0.3"
    compile "com.comphenix.attribute:AttributeStorage:0.0.2-SNAPSHOT"
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("DRONE") != null ? "drone" : "compiled") + "-" + (System.getenv("BUILD_ID") ?: "0"),
            NAME: project.name
    ]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

shadowJar {
    baseName = "facecore-gradle-all"
    classifier = ""

    relocate "com.comphenix", "org.nunnerycode.facecore.comphenix"
}

artifacts {
    archives shadowJar
    archives sourcesJar
    archives javadocJar
}

license {
    header file('HEADER')
    include "src/main/java/org/nunnerycode/facecore/**/*.java"
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "facecore-gradle"
            from components.java
        }
        shadow(MavenPublication) {
            artifactId = "facecore-gradle-all"
            from components.shadow
        }
    }
    repositories {
        maven {
            url "http://repo-topplethenun.rhcloud.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "nunnerycode-repo"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}